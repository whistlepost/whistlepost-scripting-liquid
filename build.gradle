plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.13.3'
    id 'biz.aQute.bnd.builder' version "$bndVersion"
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.apache.sling:org.apache.sling.scripting.api:2.2.0',
            'org.apache.sling:org.apache.sling.commons.classloader:1.4.4',
            'org.osgi:osgi.core:6.0.0',
            'org.osgi:org.osgi.service.component.annotations:1.3.0',
            'org.osgi:org.osgi.service.metatype.annotations:1.3.0',
            'org.osgi:org.osgi.annotation:6.0.0',
            "nl.big-o:liqp:$liqpVersion", "ua.co.k:strftime4j:$strftime4jVersion"
}

scmVersion {
    tag {
        prefix = 'whistlepost-scripting-liquid-'
    }
    versionCreator 'versionWithBranch'
    branchVersionCreator = ['main': 'simple']
    nextVersion {
        suffix = 'pre'
        separator = '-'
    }
}

sourceCompatibility = 8
targetCompatibility = 8

group = 'org.whistlepost'
version = rootProject.scmVersion.version

ext {
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        "$name"(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
            pom.withXml {
                asNode().appendNode('name', name)
                asNode().appendNode('description', description)
                asNode().appendNode('url', 'https://www.whistlepost.org')

                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('url', 'https://github.com/whistlepost/whistlepost-scripting-liquid')
                scmNode.appendNode('connection', 'scm:git@github.com:whistlepost/whistlepost-scripting-liquid.git')
                scmNode.appendNode('developerConnection', 'scm:git@github.com:whistlepost/whistlepost-scripting-liquid.git')

                def licenseNode = asNode().appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', 'Apache License')
                licenseNode.appendNode('url', 'https://raw.githubusercontent.com/whistlepost/whistlepost/master/LICENSE')
                licenseNode.appendNode('distribution', 'repo')

                def developerNode = asNode().appendNode('developers').appendNode('developer')
                developerNode.appendNode('id', 'fortuna')
                developerNode.appendNode('name', 'Ben Fortuna')
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = version.endsWith('SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots/" :
                    "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications[name]
}
